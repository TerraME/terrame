# TerraME - a software platform for multiple scale spatially-explicit dynamic modeling.
# Copyright (C) 2001-2014 INPE and TerraLAB/UFOP.
#
# This file is part of the TerraME framework.
# This framework is free software; you can redistribute it and/or
# modify it under the terms of the GNU Lesser General Public
# License as published by the Free Software Foundation; either
# version 2.1 of the License, or (at your option) any later version.
#
# You should have received a copy of the GNU Lesser General Public
# License along with this library.
#
# The authors reassure the license terms regarding the warranties.
# They specifically disclaim any warranties, including, but not limited to,
# the implied warranties of merchantability and fitness for a particular purpose.
# The framework provided hereunder is on an "as is" basis, and the authors have no
# obligation to provide maintenance, support, updates, enhancements, or modifications.
# In no event shall INPE and TerraLAB / UFOP be held liable to any party for direct,
# indirect, special, incidental, or caonsequential damages arising out of the use
# of this library and its documentation.
#
# Authors: Pedro R. Andrade (pedro.andrade@inpe.br)
#          Rodrigo Avancini
#

set(CPACK_PACKAGE_NAME "TerraME")
set(CPACK_PACKAGE_VERSION @TERRAME_VERSION_STRING@)
set(CPACK_PACKAGE_VENDOR "INPE")
set(CPACK_PACKAGE_DESCRIPTION_SUMMARY "TerraME Application")
set(CPACK_PACKAGE_VERSION_MAJOR @TERRAME_VERSION_MAJOR@)
set(CPACK_PACKAGE_VERSION_MINOR @TERRAME_VERSION_MINOR@)
set(CPACK_PACKAGE_VERSION_PATCH @TERRAME_VERSION_PATCH@)

set(CPACK_RESOURCE_FILE_LICENSE @TERRAME_ABSOLUTE_ROOT_DIR@/LICENSE)

#
# getting some variables from CMake
#
set(MSVC @MSVC@)
set(CMAKE_CL_64 @CMAKE_CL_64@)
set(APPLE @APPLE@)
set(UNIX @UNIX@)

#
# determining the binary package name depending on toolset used in the build
#
if(NOT DEFINED CPACK_SOURCE_PACKAGE_FILE_NAME)
	if(MSVC)
		if(CMAKE_CL_64)
			set(CPACK_PACKAGE_FILE_NAME "terrame-@TERRAME_VERSION_STRING@-win64")
		else()
			set(CPACK_PACKAGE_FILE_NAME "terrame-@TERRAME_VERSION_STRING@-win32")
		endif()
	elseif(APPLE)
		set(CPACK_PACKAGE_FILE_NAME "terrame-@TERRAME_VERSION_STRING@-macosx")
	elseif(UNIX)
		set(CPACK_PACKAGE_FILE_NAME "terrame-@TERRAME_VERSION_STRING@-linux64")
	else()
		message(FATAL_ERROR "terrame ERROR: unknown platform for generating install package. See terralib-cpack-options.cmake.in for more details!")
	endif()
else()
	set(CPACK_PACKAGE_FILE_NAME "terrame-@TERRAME_VERSION_STRING@-src")
endif()

#
# specific configuration for NSIS package generator
#
# variables that controls what is built
if(CPACK_GENERATOR MATCHES "NSIS")
	set(CPACK_NSIS_MUI_ICON @TERRAME_ABSOLUTE_ROOT_DIR@/res\\\\app.ico)
	set(CPACK_NSIS_MUI_UNIICON @TERRAME_ABSOLUTE_ROOT_DIR@/res\\\\app.ico)
	set(CPACK_NSIS_INSTALLED_ICON_NAME @TERRAME_ABSOLUTE_ROOT_DIR@/res\\\\app.ico)
	set(CPACK_PACKAGE_ICON @TERRAME_ABSOLUTE_ROOT_DIR@/res\\\\app.png)

	# set(CPACK_RESOURCE_FILE_README @TERRAME_ABSOLUTE_ROOT_DIR@/LICENSE)

	set(CPACK_PACKAGE_INSTALL_DIRECTORY "TerraME")

	set(CPACK_CREATE_DESKTOP_LINKS TerraME)

	set(CPACK_NSIS_PACKAGE_NAME "TerraME")
	set(CPACK_NSIS_DISPLAY_NAME "TerraME version @TERRAME_VERSION_STRING@")

	set(CPACK_NSIS_HELP_LINK "http://terrame.org")
	set(CPACK_NSIS_URL_INFO_ABOUT "http://terrame.org")
	set(CPACK_NSIS_CONTACT "pedro.andrade@inpe.br")

	if(CMAKE_CL_64)
		set(CPACK_NSIS_INSTALL_ROOT "C:")
		set(CPACK_NSIS_PACKAGE_NAME "${CPACK_PACKAGE_INSTALL_DIRECTORY}")
	else()
		set(CPACK_NSIS_INSTALL_ROOT "C:") 
		set(CPACK_NSIS_PACKAGE_NAME "${CPACK_PACKAGE_INSTALL_DIRECTORY}")
	endif()

	# To overwrite a previous installed version
	set(CPACK_PACKAGE_INSTALL_REGISTRY_KEY ${CPACK_PACKAGE_NAME}) 

	set(shortCutName "TerraME")

	set(CPACK_PACKAGE_EXECUTABLES "terrame;${shortCutName}")

	list(APPEND CPACK_NSIS_CREATE_ICONS_EXTRA 
		# "SetOutPath '\$INSTDIR\\\\bin'" #TODO: REVIEW
		"
		CreateShortCut \"$DESKTOP\\${shortCutName}.lnk\" \"$INSTDIR\\bin\\terrame.exe\";
		CreateShortCut \"$SMPROGRAMS\\$STARTMENU_FOLDER\\${shortCutName}.lnk\" \"$INSTDIR\\bin\\terrame.exe\"
		"
	)

	list(APPEND CPACK_NSIS_DELETE_ICONS_EXTRA 
		# "Delete '\$SMPROGRAMS\\\\$MUI_TEMP\\\\${shortCutName}.lnk'" # TODO: REVIEW
		"
		Delete \"$DESKTOP\\${shortCutName}.lnk\";
		Delete \"$SMPROGRAMS\\$STARTMENU_FOLDER\\${shortCutName}.lnk\"
		"
	)

	list(APPEND CPACK_NSIS_EXTRA_INSTALL_COMMANDS
		"
		WriteRegExpandStr HKLM \"SYSTEM\\CurrentControlSet\\Control\\Session Manager\\Environment\" \"TME_PATH\" \"\$INSTDIR\\bin\";
		SendMessage \${HWND_BROADCAST} \${WM_WININICHANGE} 0 \"STR:Environment\" /TIMEOUT=5000;
		"
		# SetRebootFlag true #TODO: REVIEW
	)
	
	set(CPACK_NSIS_EXTRA_UNINSTALL_COMMANDS
		"
		DeleteRegValue HKLM \"SYSTEM\\CurrentControlSet\\Control\\Session Manager\\Environment\" \"TME_PATH\";
		SendMessage \${HWND_BROADCAST} \${WM_WININICHANGE} 0 \"STR:Environment\" /TIMEOUT=5000
		"
	)
endif()
